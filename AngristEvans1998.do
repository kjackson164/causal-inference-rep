destring QTRMAR QTRBTHM AGEMAR QTRBKID SEXK SEX2ND FINGRADM GRADEM AGEM ///
AGED QTRBTHD WEEKSM WEEKSD HOURSM HOURSD INCOME1M INCOME2M INCOME1D INCOME2D ///
FAMINC TIMESMAR, replace

*** indicator for twins
gen multi2nd = (AGEQ2ND==AGEQ3RD)

*** determine out of wedlock births
gen illegit = 0
gen il_flag = 1 if QTRMAR > 0
replace QTRMAR = QTRMAR - 1 if il_flag==1

*** year of marriage
gen yom = YOBM + AGEMAR if (QTRBTHM <= QTRMAR) & (il_flag==1)
replace yom = YOBM + AGEMAR + 1 if (QTRBTHM > QTRMAR) & (il_flag==1)

*** time of marriage
gen dom_q = yom + (QTRMAR/4) if (il_flag==1)

*** time of first birth
gen do1b_q = YOBK + (QTRBKID/4) if (il_flag==1)

*** illegit if marriage after birth
replace illegit = 1 if (dom_q>do1b_q) & (il_flag==1)

*** construct sexes of first two kids
gen boy1st = (SEXK==0)
gen boy2nd = (SEX2ND==0)
gen boys2 = (SEXK==0) & (SEX2ND==0)
gen girls2 = (SEXK==1) & (SEX2ND==1)
gen samesex = (boys2==1) | (girls2==1)
gen morekids = KIDCOUNT > 2

label variable boy1st "first birth boy"
label variable samesex "first two kids are of same sex"
label variable morekids "had more than 2 kids"
label variable boys2 "first two births boys"
label variable girls2 "first two births girls"

*** construct race indicators for mom and dad
gen blackm = RACEM=="02"
gen hispm = RACEM=="12"
gen whitem = RACEM=="01"
gen othracem = 1-blackm-hispm-whitem

gen blackd = RACED=="02"
gen hispd = RACED=="12"
gen whited = RACED=="01"
gen othraced = 1-blackd-hispd-whited

label variable blackm "=1 of black"
label variable hispm "=1 if hispanic"
label variable othracem "=1 if other race (white is ref)"
label variable blackd "=1 of black"
label variable hispd "=1 if hispanic"
label variable othraced "=1 if other race (white is ref)"

*** education variable
gen educm = GRADEM-2 if (FINGRADM==2) | (FINGRADM==3)
replace educm = GRADEM-3 if (FINGRADM!=2) & (FINGRADM!=3)
replace educm = max(0,educm)
label variable educm "moms education"

gen hsgrad = (educm==12)
gen hsormore = (educm>=12)
gen moreths = (educm>12)

*** get age at birth of first child
gen YOBD = 80 - AGED if QTRBTHD==0
replace YOBD = 79 - AGED if QTRBTHD!=0

*** parents age in quarters
gen ageqm = 4*(80-YOBM)-QTRBTHM-1
gen ageqd = 4*(80-YOBD)-QTRBTHD

*** age at first birth
gen agefstm = int((ageqm-AGEQK)/4)
gen agefstd = int((ageqd-AGEQK)/4)
label variable agefstm "age of mom when first kid born"
label variable agefstd "age of dad when first kid born"

*** calculate mom and dad labor supply variables
*** worked indicators
gen workedm = (WEEKSM>0)
gen workedd = (WEEKSD>0)

*** income from wages and self employment, not counting negative profits
gen incomed = INCOME1D + max(0,INCOME2D)
gen incomem = INCOME1M + max(0,INCOME2M)

** deflate wages **;
replace incomem = incomem*2.099173554
replace incomed = incomed*2.099173554

*** family income1m*2
replace FAMINC = FAMINC*2.099173554
gen famincl = log(max(FAMINC,1))

*** non mothers income
gen nonmomi = FAMINC-INCOME1M*2.099173554
gen nonmomil = log(max(1,nonmomi))

label variable workedm "mom worked last year"
label variable workedd "dad worked last year"
label variable famincl "log family income"
label variable nonmomi "income not generated by mom"
label variable incomed "dads labor income"
label variable incomem "moms labor income"


*** generate sample indicators
gen msample = 0
replace msample = 1 if (AGED!=.) & ///
						(TIMESMAR==1) & ///
						(MARITAL=="0") & ///
						(illegit==0) & ///
						(agefstd>=15) & ///
						(agefstm>=15)
						
gen zsample = 0
replace zsample = 1 if (AGEM<=35) & ///
						(AGEM>=21) & ///
						(KIDCOUNT>=2) & ///
						(AGEQ2ND>4) & ///
						(agefstm>=15) & ///
						(ASEX=="0") & ///
						(AAGE=="0") & ///
						(AQTRBRTH=="0") & ///
						(ASEX2ND=="0") & ///
						(AAGE2ND=="0")
						

////////////////////////////////////// TABLE 4 /////////////////////////////////////

***** 1980 BY SEX *****
***COLUMN 1***
// tabstat AGEM agefstm blackm whitem othracem hispm educm if zsample==1, by(samesex) statistics(mean sd)

local myresults "diff = (r(mu_2)-r(mu_1)) standarderror =  r(se)"
display "`myresults'"

table (command) (result) if zsample==1, ///
command(`myresults' : ttest AGEM, by(samesex)) ///
command(`myresults' : ttest agefstm, by(samesex)) ///
command(`myresults' : ttest blackm, by(samesex)) ///
command(`myresults' : ttest whitem, by(samesex)) ///
command(`myresults' : ttest othracem, by(samesex)) ///
command(`myresults' : ttest hispm, by(samesex)) ///
command(`myresults' : ttest educm, by(samesex)) ///
nformat(%6.4f  diff) ///        
nformat(%6.5f  standarderror)

collect export table1.tex, tableonly replace

***** 1990 BY SEX *****
***COLUMN 2***

***** 1980 BY TWINS *****
***COLUMN 3***

local myresults "diff = (r(mu_2)-r(mu_1)) standarderror =  r(se)"
display "`myresults'"

table (command) (result) if zsample==1, ///
command(`myresults' : ttest AGEM, by(multi2nd)) ///
command(`myresults' : ttest agefstm, by(multi2nd)) ///
command(`myresults' : ttest blackm, by(multi2nd)) ///
command(`myresults' : ttest whitem, by(multi2nd)) ///
command(`myresults' : ttest othracem, by(multi2nd)) ///
command(`myresults' : ttest hispm, by(multi2nd)) ///
command(`myresults' : ttest educm, by(multi2nd)) ///
nformat(%6.4f  diff) ///        
nformat(%6.5f  standarderror)

collect export table2.tex, tableonly replace

						
////////////////////////////////////// TABLE 7 /////////////////////////////////////

***** ALL WOMEN *****
***COLUMN 1***
est clear
local m=1
foreach var of varlist workedm WEEKSM HOURSM incomem famincl {
    eststo model1_`m': quietly reg `var' morekids AGEM agefstm boy1st boy2nd blackm hispm othracem if zsample==1
local m `++m'
}
esttab model1_*, keep(morekids) se replace

***COLUMN 2***
local m=1
foreach var of varlist workedm WEEKSM HOURSM incomem famincl {
    eststo model2_`m': quietly ivregress 2sls `var' (morekids=samesex) AGEM agefstm boy1st boy2nd blackm hispm othracem if zsample==1
local m `++m'
}
esttab model2_*, keep(morekids) se replace

***COLUMN 3***
/// alternative to two instruments
/// 	gen boysgirls2 = 0
/// 	replace boysgirls2 = 0
/// 	replace boysgirls2 = 1 if (boys2==1 | girls2==1)

local m=1
foreach var of varlist workedm WEEKSM HOURSM incomem famincl {
    eststo model3_`m': quietly ivregress 2sls `var' (morekids=boys2 girls2) AGEM agefstm boy1st blackm hispm othracem if zsample==1
	estat overid
local m `++m'
}
esttab model3_*, keep(morekids) se replace

***** MARRIED WOMEN *****
***COLUMN 4***
local m=1
foreach var of varlist workedm WEEKSM HOURSM incomem famincl nonmomil {
    eststo model4_`m': quietly reg `var' morekids AGEM agefstm boy1st boy2nd blackm hispm othracem if (zsample==1 & msample==1)
local m `++m'
}
esttab model4_*, keep(morekids) se replace

***COLUMN 5***
local m=1
foreach var of varlist workedm WEEKSM HOURSM incomem famincl nonmomil {
    eststo model5_`m': quietly ivregress 2sls `var' (morekids=samesex) AGEM agefstm boy1st blackm hispm othracem if (zsample==1 & msample==1)
local m `++m'
}
esttab model5_*, keep(morekids) se replace

***COLUMN 6***
local m=1
foreach var of varlist workedm WEEKSM HOURSM incomem famincl nonmomil {
    eststo model6_`m': quietly ivregress 2sls `var' (morekids=boys2 girls2) AGEM agefstm boy1st blackm hispm othracem if (zsample==1 & msample==1)
	estat overid
local m `++m'
}
esttab model6_*, keep(morekids) se replace

***** HUSBANDS OF MARRIED WOMEN *****
***COLUMN 7***
local m=1
foreach var of varlist workedd WEEKSD HOURSD incomed {
    eststo model7_`m': quietly reg `var' morekids AGEM agefstm boy1st boy2nd blackm hispm othracem if (zsample==1 & msample==1)
local m `++m'
}
esttab model7_*, keep(morekids) se replace

***COLUMN 8***
local m=1
foreach var of varlist workedd WEEKSD HOURSD incomed {
    eststo model8_`m': quietly ivregress 2sls `var' (morekids=samesex) AGEM agefstm boy1st boy2nd blackm hispm othracem if (zsample==1 & msample==1)
local m `++m'
}
esttab model8_*, keep(morekids) se replace

***COLUMN 9***
local m=1
foreach var of varlist workedd WEEKSD HOURSD incomed {
    eststo model9_`m': quietly ivregress 2sls `var' (morekids=boys2 girls2) AGEM agefstm boy1st blackm hispm othracem if (zsample==1 & msample==1)
	estat overid
local m `++m'
}
esttab model9_*, keep(morekids) se replace
